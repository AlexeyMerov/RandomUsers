apply plugin: 'com.android.application'

android {
    compileSdkVersion 27
    defaultConfig {
        applicationId "com.alexeymerov.randomusers"
        minSdkVersion 21
        targetSdkVersion 27
        versionCode 1
        versionName "1.0"
        vectorDrawables.useSupportLibrary = true
    }
    buildTypes {
        release {
            debuggable false
            jniDebuggable false
            renderscriptDebuggable false
            pseudoLocalesEnabled false
            zipAlignEnabled true

            minifyEnabled true
            shrinkResources true
            useProguard true

            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            buildOutputApkName(applicationVariants)

//            ndk {
//                abiFilters "arm64-v8a", "armeabi-v7a"
//            }
        }

        debug {
            minifyEnabled false
            shrinkResources false
            useProguard false
            buildOutputApkName(applicationVariants)
        }
    }

//    lintOptions {
//        lintConfig file("lint.xml")
//        warningsAsErrors = true
//        baseline file("baseline-lint.xml")
//    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

def buildOutputApkName(DomainObjectSet domainObjectSet) {
    domainObjectSet.all { variant ->
        variant.outputs.all { output ->
            outputFileName = "RandomUsers_${variant.baseName}_" + new Date().format('(dd.MM.yy)_(HH)')
        }
    }
}

ext {
    supportLib = '27.1.1'
    retrofit = '2.3.0'
    room = '1.1.0-beta2'
    glide = '4.6.1'
    playServices = '12.0.1'
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    //support
    implementation "com.android.support:appcompat-v7:${supportLib}"
    implementation "com.android.support:design:${supportLib}"
    implementation "com.android.support:recyclerview-v7:${supportLib}"
    implementation "com.android.support:support-v13:${supportLib}"
    implementation 'com.android.support.constraint:constraint-layout:1.1.0-beta6'

    //dagger
    implementation 'com.google.dagger:dagger:2.12'
    annotationProcessor 'com.google.dagger:dagger-compiler:2.12'

    //retrofit
    implementation "com.squareup.retrofit2:retrofit:${retrofit}"
    implementation "com.squareup.retrofit2:converter-moshi:${retrofit}"
    implementation "com.squareup.retrofit2:adapter-rxjava2:${retrofit}"
    implementation 'com.squareup.okhttp3:logging-interceptor:3.8.0'

    //view binding
    implementation 'com.jakewharton:butterknife:8.8.1'
    annotationProcessor 'com.jakewharton:butterknife-compiler:8.8.1'

    // rx
    implementation 'io.reactivex.rxjava2:rxkotlin:2.0.0'
    implementation 'io.reactivex.rxjava2:rxandroid:2.0.2'
    implementation 'io.reactivex.rxjava2:rxjava:2.1.9'

    // ViewModel and LiveData
    implementation "android.arch.lifecycle:extensions:1.1.1"
    implementation "android.arch.lifecycle:common-java8:1.1.1"

    // Room
    implementation "android.arch.persistence.room:runtime:${room}"
    annotationProcessor "android.arch.persistence.room:compiler:${room}"
    debugImplementation 'com.amitshekhar.android:debug-db:1.0.3'

    // glide
    implementation "com.github.bumptech.glide:glide:$glide"
    annotationProcessor "com.github.bumptech.glide:compiler:$glide"

    //logging
    implementation 'com.facebook.stetho:stetho:1.5.0'
    implementation 'com.facebook.stetho:stetho-okhttp3:1.5.0'
    implementation 'com.squareup.okhttp3:logging-interceptor:3.8.0'

    //permissions
    implementation 'com.tbruyelle.rxpermissions2:rxpermissions:0.9.5@aar'

//    implementation 'com.google.firebase:firebase-core:15.0.0'
    implementation "com.google.android.gms:play-services-auth:${playServices}"
    implementation "com.google.android.gms:play-services-maps:${playServices}"

}

apply plugin: 'com.google.gms.google-services'